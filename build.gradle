version '0.9-BETA'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.1'
    }
}


allprojects {
    apply plugin: 'jacoco'
    apply plugin: 'checkstyle'


    checkstyle {
        toolVersion = "8.3"
        configDir = file("$project.rootDir/conf/checkstyle")
    }


    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'org.junit.platform.gradle.plugin'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    compileJava.options.encoding = "UTF-8"
    compileTestJava.options.encoding = "UTF-8"

    dependencies {
        testCompile 'org.junit.jupiter:junit-jupiter-api:5.0.1'
        testCompile 'org.assertj:assertj-core:3.8.0'
        testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.0.1'
    }

    jacoco {
        toolVersion = '0.7.9'
        applyTo junitPlatformTest
    }

    junitPlatformTest
}


task codeCoverageReport(type: JacocoReport, dependsOn: 'waitForSubProject') {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.enabled true
        xml.destination file("$buildDir/reports/jacoco/report.xml")
        html.enabled true
        html.destination file("$buildDir/coverage_report")
        csv.enabled false
    }

    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    // No coverage for the UI,
                    // Testing this part is really hard,
                    // and quite useless.
                    '**/ui/**',
            ])
        })
    }
}

task waitForSubProject {
    subprojects.each { dependsOn("${it.name}:junitPlatformTest") }
}

task check(dependsOn: 'codeCoverageReport')